<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:s="library://ns.adobe.com/flex/spark"
	xmlns:view="view.*"
	implements="mx.managers.IFocusManagerComponent, interfaces.ISurface"
	mouseDown="editingSurface_mouseDownHandler(event)"
	dragComplete="editingSurface_dragCompleteHandler(event)"
	dragEnter="editingSurface_dragEnterOrOverHandler(event)"
	dragOver="editingSurface_dragEnterOrOverHandler(event)"
	dragExit="editingSurface_dragExitHandler(event)"
	dragDrop="editingSurface_dragDropHandler(event)"
	keyDown="editingSurface_keyDownHandler(event)">

	<fx:Metadata>
		[Style(name="dragImageAlpha",inherit="true",type="Number")]
		[Event(name="change",type="flash.events.Event")]
		[Event(name="propertyEditorItemAdding",type="view.suportClasses.events.PropertyEditorChangeEvent")]
		[Event(name="propertyEditorItemMoved",type="view.suportClasses.events.PropertyEditorChangeEvent")]
	</fx:Metadata>

	<fx:Declarations>
		<view:ResizeHandles id="resizer"/>
	</fx:Declarations>

	<fx:Script><![CDATA[
		import interfaces.IComponent;
		import mx.containers.GridItem;
		import mx.containers.GridRow;
		import mx.controls.scrollClasses.ScrollBar;
		import mx.core.BitmapAsset;
		import mx.core.Container;
		import mx.core.DragSource;
		import mx.core.IUIComponent;
		import mx.core.IVisualElement;
		import mx.core.IVisualElementContainer;
		import mx.core.UIComponent;
		import mx.events.DragEvent;
		import mx.managers.DragManager;
		import mx.managers.IFocusManagerComponent;
		import mx.managers.PopUpManager;
		import spark.components.SkinnableContainer;
		import spark.components.supportClasses.ScrollBarBase;
		import spark.components.supportClasses.SkinnableContainerBase;
		import spark.containers.Navigator;
		import spark.events.IndexChangeEvent;
		import utils.EditingSurfaceReader;
		import utils.EditingSurfaceWriter;
		import utils.GenericUtils;
		import view.interfaces.IHistorySurfaceComponent;
		import view.interfaces.IInitializeAfterAddedComponent;
		import view.interfaces.IMainApplication;
		import view.interfaces.IMultiViewContainer;
		import view.interfaces.INonDeletableSurfaceComponent;
		import view.interfaces.ISurfaceComponent;
		import view.primeFaces.supportClasses.GridBase;
		import view.primeFaces.surfaceComponents.components.Div;
		import view.primeFaces.surfaceComponents.components.Grid;
		import view.primeFaces.surfaceComponents.components.PanelGrid;
		import view.suportClasses.PropertyChangeReference;
		import view.suportClasses.events.PropertyEditorChangeEvent;
		import view.suportClasses.events.SurfaceComponentEvent;
		public var organizer:ComponentsOrganizer;
		private static const SMALL_NUDGE:int = 1;
		private static const LARGE_NUDGE:int = 10;
		private var _dropOverlay:UIComponent = new UIComponent();
		private var _selectedItem:ISurfaceComponent = null;
		private var _wasLoaded:Boolean = false;
		private var _hasChanged:Boolean = false;
		private var _visualEditorType:String;
		private var _isDominoProject:Boolean = false;
		public function set visualEditorType(value:String):void
		{
			_visualEditorType = value;
		}
		public function get hasChanged():Boolean
		{
			return _hasChanged;
		}
		public function set hasChanged(value:Boolean):void
		{
			if (_hasChanged != value)
			{
				_hasChanged = value;
				dispatchEvent(new Event(Event.CHANGE));
			}
		}
		[Bindable("change")]
		public function get selectedItem():ISurfaceComponent
		{
			return this._selectedItem;
		}
		public function set selectedItem(value:ISurfaceComponent):void
		{
			if (this._selectedItem === value)
			{
				return;
			}
			if (this._selectedItem) this._selectedItem.isSelected = false;
			this._selectedItem = value;
			if (this._selectedItem && !this.resizer.parent)
			{
				PopUpManager.addPopUp(this.resizer, this, false);
			}
			else if (!this._selectedItem && this.resizer.parent)
			{
				PopUpManager.removePopUp(this.resizer);
			}
			if (this._selectedItem) this._selectedItem.isSelected = true;
			this.resizer.target = this._selectedItem;
			this.dispatchEvent(new Event(Event.CHANGE));
		}
		public function get isVisualEditorFlex():Boolean
		{
			return (_visualEditorType == 'flex');
		}
		public function get isVisualEditorDomino():Boolean
		{
			return (_visualEditorType == 'domino');
		}
		public function setResizerVisible(value:Boolean):void
		{
			if (this.resizer) this.resizer.visible = value;
		}
		public function toXML():XML
		{
			hasChanged = false;
			_wasLoaded = this.numElements > 0;
			return EditingSurfaceWriter.toXML(this, _visualEditorType);
		}
		CONFIG::MOONSHINE
		{
			public function toCode():XML
			{
				return EditingSurfaceWriter.toCode(this);
			}
			/**
			 * overloading the toCode method, when domino project
			 * call this method will pass the project_type parameter.
			 * Only domino project will call this method ,this will keep old method work fine.
			 *
			 */
			public function toDominoCode():XML
			{
				_isDominoProject = true ;
				return EditingSurfaceWriter.toDominoCode(this);
			}
			
		}
		public function fromXML(xml:XML):void
		{
			this.deleteAll();
			EditingSurfaceReader.fromXML(this, xml, _visualEditorType);
			_wasLoaded = true;
			if (!isVisualEditorFlex) this.callLater(organizer.loadFirstData, [this]);
		}
		public function deleteAll():void
		{
			var elementCount:int = this.numElements;
			for (var i:int = elementCount - 1; i >= 0; i--)
			{
				var element:ISurfaceComponent = this.getElementAt(i) as ISurfaceComponent;
				if (element === null)
				{
					continue;
				}
				this.deleteItem(element);
			}
			hasChanged = false;
		}
		/**
		 * Do not call removeElement(). Calling deleteItem() will ensure that
		 * everything is cleaned up properly.
		 */
		public function deleteItem(item:ISurfaceComponent):void
		{
			if (item is INonDeletableSurfaceComponent || !item.stage)
			{
				return;
			}
			item.dispatchEvent(new Event(PropertyEditorChangeEvent.PROPERTY_EDITOR_ITEM_DELETING));
			// @devsena
			// missing event removals
			item.removeEventListener(MouseEvent.MOUSE_MOVE, object_mouseMoveHandler);
			item.stage.removeEventListener(MouseEvent.MOUSE_UP, stage_mouseUpHandler);
			var container:IVisualElementContainer = IVisualElementContainer(item.parent);
			if (container.getElementIndex(IVisualElement(item)) > -1)
			{
				if (!isVisualEditorFlex) organizer.removeDroppedElement(item.owner as IVisualElement, item as IVisualElement);
				container.removeElement(IVisualElement(item));
			}
			if (item is IMultiViewContainer)
			{
				var multiViewContainer:IMultiViewContainer = IMultiViewContainer(item);
				multiViewContainer.removeEventListener(IndexChangeEvent.CHANGE, multiViewContainer_indexChangeHandler);
				this.cleanupContainer(multiViewContainer.selectedView);
			}
			else if (item is PanelGrid)
			{
				this.cleanupGridComponent((item as PanelGrid).body as GridBase);
			}
			else if (item is SkinnableContainer)
			{
				this.cleanupContainer(SkinnableContainer(item));
			}
			else if (item is Navigator)
			{
				this.cleanupContainer(item as SkinnableContainerBase);
			}
			else if (item is Grid)
			{
				this.cleanupGridComponent(item as GridBase);
			}
			this.selectedItem = null;
			hasChanged = this.numElements > 0 && _wasLoaded;
			// once an item deletes stage loose focus to
			// listen any keypress event, i.e. Ctrl+Z/Y
			this.setFocus();
		}
		private function prepareContainer(container:UIComponent):void
		{
			if (container === null)
			{
				return;
			}
			if (!container.hasEventListener(SurfaceComponentEvent.ComponentAdded))
			{
				container.addEventListener(SurfaceComponentEvent.ComponentAdded, onSurfaceComponentAdded);
			}
			if (!container.hasEventListener(SurfaceComponentEvent.ComponentRemoved))
			{
				container.addEventListener(SurfaceComponentEvent.ComponentRemoved, onSurfaceComponentRemoved);
			}
			container.addEventListener(DragEvent.DRAG_ENTER, skinnableContainer_dragEnterOrOverHandler);
			container.addEventListener(DragEvent.DRAG_OVER, skinnableContainer_dragEnterOrOverHandler);
			container.addEventListener(DragEvent.DRAG_DROP, skinnableContainer_dragDropHandler);
		}
		private function cleanupContainer(container:UIComponent):void
		{
			if (container === null)
			{
				return;
			}
			container.removeEventListener(DragEvent.DRAG_ENTER, skinnableContainer_dragEnterOrOverHandler);
			container.removeEventListener(DragEvent.DRAG_OVER, skinnableContainer_dragEnterOrOverHandler);
			container.removeEventListener(DragEvent.DRAG_DROP, skinnableContainer_dragDropHandler);
		}
		private function onSurfaceComponentAdded(event:SurfaceComponentEvent):void
		{
			if (event.items)
			{
				for (var i:int = 0; i < event.items.length; i++)
				{
					var surfaceComponent:IUIComponent = event.items[i] as IUIComponent;
					surfaceComponent.addEventListener(DragEvent.DRAG_ENTER, skinnableContainer_dragEnterOrOverHandler);
					surfaceComponent.addEventListener(DragEvent.DRAG_OVER, skinnableContainer_dragEnterOrOverHandler);
					surfaceComponent.addEventListener(DragEvent.DRAG_DROP, skinnableContainer_dragDropHandler);
				}
			}
		}
		private function onSurfaceComponentRemoved(event:SurfaceComponentEvent):void
		{
			if (event.items)
			{
				for (var i:int = 0; i < event.items.length; i++)
				{
					var surfaceComponent:IUIComponent = event.items[i];
					surfaceComponent.removeEventListener(DragEvent.DRAG_ENTER, skinnableContainer_dragEnterOrOverHandler);
					surfaceComponent.removeEventListener(DragEvent.DRAG_OVER, skinnableContainer_dragEnterOrOverHandler);
					surfaceComponent.removeEventListener(DragEvent.DRAG_DROP, skinnableContainer_dragDropHandler);
				}
			}
		}
		private function prepareGridComponent(grid:GridBase):void
		{
			if (grid === null)
			{
				return;
			}
			grid.addEventListener(SurfaceComponentEvent.ComponentAdded, onSurfaceComponentAdded);
			grid.addEventListener(SurfaceComponentEvent.ComponentRemoved, onSurfaceComponentRemoved);
			var gridNumElements:int = grid.numElements;
			for (var row:int = 0; row < gridNumElements; row++)
			{
				var gridRow:GridRow = grid.getElementAt(row) as GridRow;
				if (gridRow)
				{
					var gridColNumElements:int = gridRow.numElements;
					for (var col:int = 0; col < gridColNumElements; col++)
					{
						var gridCol:GridItem = gridRow.getElementAt(col) as GridItem;
						if (gridCol)
						{
							var div:Div = gridCol.getElementAt(0) as Div;
							div.addEventListener(DragEvent.DRAG_ENTER, skinnableContainer_dragEnterOrOverHandler);
							div.addEventListener(DragEvent.DRAG_OVER, skinnableContainer_dragEnterOrOverHandler);
							div.addEventListener(DragEvent.DRAG_DROP, skinnableContainer_dragDropHandler);
						}
					}
				}
			}
		}
		private function cleanupGridComponent(grid:GridBase):void
		{
			if (grid === null)
			{
				return;
			}
			grid.removeEventListener(SurfaceComponentEvent.ComponentAdded, onSurfaceComponentAdded);
			grid.removeEventListener(SurfaceComponentEvent.ComponentRemoved, onSurfaceComponentRemoved);
			var gridNumElements:int = grid.numElements;
			for (var row:int = 0; row < gridNumElements; row++)
			{
				var gridRow:GridRow = grid.getElementAt(row) as GridRow;
				if (gridRow)
				{
					var gridColNumElements:int = gridRow.numElements;
					for (var col:int = 0; col < gridColNumElements; col++)
					{
						var gridCol:GridItem = gridRow.getElementAt(col) as GridItem;
						if (gridCol)
						{
							var div:Div = gridCol.getElementAt(0) as Div;
							div.removeEventListener(DragEvent.DRAG_ENTER, skinnableContainer_dragEnterOrOverHandler);
							div.removeEventListener(DragEvent.DRAG_OVER, skinnableContainer_dragEnterOrOverHandler);
							div.removeEventListener(DragEvent.DRAG_DROP, skinnableContainer_dragDropHandler);
						}
					}
				}
			}
		}
		private function showDropOverlay(dropTarget:UIComponent):void
		{
			var currentTarget:DisplayObjectContainer = dropTarget;
			//we need to calculated the visible rectangular region where we can
			//drop into the current container
			var origin:Point = new Point(0, 0);
			var globalPosition:Point = dropTarget.localToGlobal(origin);
			var rect:Rectangle = new Rectangle(globalPosition.x, globalPosition.y, dropTarget.width, dropTarget.height);
			while (currentTarget !== this)
			{
				var isSkinnableContainer:Boolean = currentTarget is SkinnableContainer;
				if (isSkinnableContainer || currentTarget is IMainApplication)
				{
					var otherGroup:UIComponent = isSkinnableContainer ? SkinnableContainer(currentTarget).contentGroup : currentTarget as UIComponent;
					globalPosition = otherGroup.localToGlobal(origin);
					var newRect:Rectangle = new Rectangle(globalPosition.x, globalPosition.y, otherGroup.width, otherGroup.height);
					rect = rect.intersection(newRect);
				}
				currentTarget = currentTarget.parent;
			}
			this._dropOverlay.mouseEnabled = false;
			this._dropOverlay.mouseChildren = false;
			this._dropOverlay.x = rect.x;
			this._dropOverlay.y = rect.y;
			this._dropOverlay.graphics.clear();
			this._dropOverlay.graphics.beginFill(this.getStyle("themeColor"), this.getStyle("dropOverlayAlpha"));
			this._dropOverlay.graphics.drawRect(0, 0, rect.width, rect.height);
			this._dropOverlay.graphics.endFill();
			PopUpManager.addPopUp(this._dropOverlay, dropTarget, false);
		}
		private function editingSurface_keyDownHandler(event:KeyboardEvent):void
		{
			if (DragManager.isDragging)
			{
				return;
			}
			if (this._selectedItem === null)
			{
				return;
			}
			if (event.keyCode === Keyboard.BACKSPACE || event.keyCode === Keyboard.DELETE)
			{
				this.deleteItem(this._selectedItem);
			}
			if (event.keyCode === Keyboard.LEFT)
			{
				if (event.shiftKey)
				{
					this._selectedItem.x -= LARGE_NUDGE;
				}
				else
				{
					this._selectedItem.x -= SMALL_NUDGE;
				}
			}
			if (event.keyCode === Keyboard.RIGHT)
			{
				if (event.shiftKey)
				{
					this._selectedItem.x += LARGE_NUDGE;
				}
				else
				{
					this._selectedItem.x += SMALL_NUDGE;
				}
			}
			if (event.keyCode === Keyboard.UP)
			{
				if (event.shiftKey)
				{
					this._selectedItem.y -= LARGE_NUDGE;
				}
				else
				{
					this._selectedItem.y -= SMALL_NUDGE;
				}
			}
			if (event.keyCode === Keyboard.DOWN)
			{
				if (event.shiftKey)
				{
					this._selectedItem.y += LARGE_NUDGE;
				}
				else
				{
					this._selectedItem.y++;
				}
			}
		}
		private function editingSurface_mouseDownHandler(event:MouseEvent):void
		{
			if (event.isDefaultPrevented())
			{
				return;
			}
			this.selectedItem = null;
		}
		private function editingSurface_dragExitHandler(event:DragEvent):void
		{
			if (this._dropOverlay.isPopUp)
			{
				PopUpManager.removePopUp(this._dropOverlay);
			}
		}
		private function editingSurface_dragEnterOrOverHandler(event:DragEvent):void
		{
			if (event.isDefaultPrevented())
			{
				return;
			}
			var dragSource:DragSource = event.dragSource;
			if (!dragSource.hasFormat(ComponentListItemRenderer.COMPONENT_DRAG_FORMAT))
			{
				return;
			}
			this.showDropOverlay(this);
			DragManager.acceptDragDrop(this);
			DragManager.showFeedback(DragManager.MOVE);
			if (event.dragInitiator !== this)
			{
				this.selectedItem = null;
			}
		}
		private function editingSurface_dragCompleteHandler(event:DragEvent):void
		{
			this.resizer.mouseEnabled = true;
			this.resizer.mouseChildren = true;
			if (this._dropOverlay.isPopUp)
			{
				PopUpManager.removePopUp(this._dropOverlay);
			}
		}
		private function dragDropHandler(event:DragEvent):void
		{
			if (this._dropOverlay.isPopUp)
			{
				PopUpManager.removePopUp(this._dropOverlay);
			}
			var dropTarget:IVisualElementContainer = IVisualElementContainer(event.currentTarget);
			var coorinateSystem:DisplayObject = DisplayObject(dropTarget);
			if (dropTarget is SkinnableContainer)
			{
				coorinateSystem = SkinnableContainer(dropTarget).contentGroup;
			}
			var dragSource:DragSource = event.dragSource;
			var offset:Point = Point(dragSource.dataForFormat(ComponentListItemRenderer.OFFSET_DRAG_FORMAT));
			var object:ISurfaceComponent = ISurfaceComponent(dragSource.dataForFormat(ComponentListItemRenderer.COMPONENT_DRAG_FORMAT));
			var position:Point = new Point(event.stageX - offset.x, event.stageY - offset.y);
			position = coorinateSystem.globalToLocal(position);
			object.x = position.x;
			object.y = position.y;
			this.addItem(object);
			//this will either add a new object or move an existing object to the top
			if (object.parent !== dropTarget)
			{
				dropTarget.addElement(IVisualElement(object));
				if (!isVisualEditorFlex) organizer.addDroppedElement(dropTarget as IVisualElement, object as IVisualElement);
				// some component may have processing should occur after
				// being added to the editor
				if (object is IInitializeAfterAddedComponent) (object as IInitializeAfterAddedComponent).componentAddedToEditor();
				// at this point property editor not created thus we send the 
				// event through EditingSurface
				if (object is IHistorySurfaceComponent)
				{
					var tmpChangeRef:PropertyChangeReference = new PropertyChangeReference(object as IHistorySurfaceComponent);
					tmpChangeRef.fieldClassIndexToParent = IVisualElementContainer(object.owner).getElementIndex(object as IVisualElement);
					tmpChangeRef.fieldClassParent = object.owner as IVisualElementContainer;
					this.dispatchEvent(new PropertyEditorChangeEvent(PropertyEditorChangeEvent.PROPERTY_EDITOR_ITEM_ADDING, tmpChangeRef));
				}
			}
			this.selectedItem = object;
			this.setFocus();
			if (dropTarget is IMainApplication)
			{
				(dropTarget as UIComponent).invalidateDisplayList();
			}
			hasChanged = true;
		}
		private function editingSurface_dragDropHandler(event:DragEvent):void
		{
			if (event.isDefaultPrevented())
			{
				return;
			}
			event.preventDefault();
			this.dragDropHandler(event);
		}
		private function skinnableContainer_dragEnterOrOverHandler(event:DragEvent):void
		{
			if (event.isDefaultPrevented())
			{
				return;
			}
			var dragSource:DragSource = event.dragSource;
			if (!dragSource.hasFormat(ComponentListItemRenderer.COMPONENT_DRAG_FORMAT))
			{
				return;
			}
			var object:ISurfaceComponent = ISurfaceComponent(dragSource.dataForFormat(ComponentListItemRenderer.COMPONENT_DRAG_FORMAT));
			var container:UIComponent = event.currentTarget as UIComponent;
			if (object.contains(container))
			{
				//don't allow a container to be dropped on itself or one of its
				//children!
				return;
			}
			var contentGroup:UIComponent = null;
			if (container is SkinnableContainer || container is Navigator)
			{
				contentGroup = container["contentGroup"];
			}
			else if (container is IMainApplication || container is Container)
			{
				contentGroup = container;
			}
			var position:Point = new Point(event.stageX, event.stageY);
			position = contentGroup.globalToLocal(position);
			if (position.x >= 0 && position.y >= 0 &&
					position.x < contentGroup.width &&
					position.y < contentGroup.height)
			{
				this.showDropOverlay(contentGroup);
				DragManager.acceptDragDrop(container);
				DragManager.showFeedback(DragManager.MOVE);
			}
		}
		private function skinnableContainer_dragDropHandler(event:DragEvent):void
		{
			if (event.isDefaultPrevented())
			{
				return;
			}
			event.preventDefault();
			this.dragDropHandler(event);
		}
		private function object_focusInHandler(event:FocusEvent):void
		{
			event.preventDefault();
		}
		private function object_mouseDownHandler(event:MouseEvent):void
		{
			if (event.isDefaultPrevented())
			{
				//if this object is a container, one of its children might
				//prevent the container from being dragged
				return;
			}
			var target:DisplayObject = DisplayObject(event.target);
			while (target !== event.currentTarget)
			{
				if (target is ScrollBarBase || target is ScrollBar)
				{
					//allow scroll bars to be dragged
					return;
				}
				target = target.parent;
			}
			event.preventDefault();
			var object:ISurfaceComponent = ISurfaceComponent(event.currentTarget);
			if (isVisualEditorFlex)
			{
				object.addEventListener(MouseEvent.MOUSE_MOVE, object_mouseMoveHandler);
				object.stage.addEventListener(MouseEvent.MOUSE_UP, stage_mouseUpHandler);
			}
			this.selectedItem = object;
		}
		private function object_mouseMoveHandler(event:MouseEvent):void
		{
			this._selectedItem.removeEventListener(MouseEvent.MOUSE_MOVE, object_mouseMoveHandler);
			this._selectedItem.stage.removeEventListener(MouseEvent.MOUSE_UP, stage_mouseUpHandler);
			var dragSource:DragSource = new DragSource();
			dragSource.addData(this._selectedItem, ComponentListItemRenderer.COMPONENT_DRAG_FORMAT);
			var offset:Point = new Point(this._selectedItem.mouseX, this._selectedItem.mouseY);
			dragSource.addData(offset, ComponentListItemRenderer.OFFSET_DRAG_FORMAT);
			this.resizer.mouseEnabled = false;
			this.resizer.mouseChildren = false;
			//this is required in Flash Player because there will be an error
			//for using removeChild() instead of removeElement()
			var data:BitmapData = new BitmapData(this._selectedItem.width, this.selectedItem.height);
			data.draw(this._selectedItem);
			var dragImage:BitmapAsset = new BitmapAsset(data);
			//the first offsets are for Flash Player
			var dragOffsetX:Number = -this.mouseX + offset.x;
			var dragOffsetY:Number = -this.mouseY + offset.y;
			if (Capabilities.playerType === "Desktop")
			{
				//the offset in AIR should be different
				dragOffsetX = this.mouseX - offset.x;
				dragOffsetY = this.mouseY - offset.y;
			}
			DragManager.doDrag(this, dragSource, event, dragImage,
					dragOffsetX, dragOffsetY,
					this.getStyle("dragImageAlpha"));
			_hasChanged = true;
		}
		private function stage_mouseUpHandler(event:MouseEvent):void
		{
			this._selectedItem.removeEventListener(MouseEvent.MOUSE_MOVE, object_mouseMoveHandler);
			this._selectedItem.stage.removeEventListener(MouseEvent.MOUSE_UP, stage_mouseUpHandler);
		}
		private function multiViewContainer_indexChangeHandler(event:IndexChangeEvent):void
		{
			var container:IMultiViewContainer = IMultiViewContainer(event.currentTarget);
			var oldView:SkinnableContainer = container.getViewAt(event.oldIndex);
			this.cleanupContainer(oldView);
			var newView:SkinnableContainer = container.selectedView;
			this.prepareContainer(newView);
		}
		public function addItem(item:IComponent):void
		{
			if (item is IFocusManagerComponent)
			{
				IFocusManagerComponent(item).focusEnabled = false;
			}
			(item as ISurfaceComponent).addEventListener(MouseEvent.MOUSE_DOWN, object_mouseDownHandler);
			(item as ISurfaceComponent).addEventListener(FocusEvent.FOCUS_IN, object_focusInHandler);
			var itemContainer:SkinnableContainer = null;
			if (item is IMultiViewContainer)
			{
				var multiViewContainer:IMultiViewContainer = IMultiViewContainer(item);
				multiViewContainer.addEventListener(IndexChangeEvent.CHANGE, multiViewContainer_indexChangeHandler);
				itemContainer = multiViewContainer.selectedView;
			}
			else if (item is PanelGrid)
			{
				this.prepareGridComponent((item as PanelGrid).body as GridBase);
			}
			else if (item is SkinnableContainer)
			{
				itemContainer = SkinnableContainer(item);
			}
			else if (item is Grid)
			{
				this.prepareGridComponent(item as Grid);
				return;
			}
			else if (item is Navigator)
			{
				this.prepareContainer(item as SkinnableContainerBase);
				return;
			}
			else if (item is IMainApplication)
			{
				this.prepareContainer(item as UIComponent);
				return;
			}
			this.prepareContainer(itemContainer);
		}
		]]></fx:Script>
</s:Group>