<?xml version="1.0" encoding="utf-8"?>
<suportclasses:BasePropertyEditorForm
        xmlns:fx="http://ns.adobe.com/mxml/2009"
        xmlns:s="library://ns.adobe.com/flex/spark"
        xmlns:suportclasses="view.suportClasses.*"
        skinClass="skins.FormSkin"
        creationComplete="onTabFormCreationComplete(event)">
	<fx:Script>
		<![CDATA[
			
			import mx.binding.utils.BindingUtils;
			import mx.binding.utils.ChangeWatcher;
			import mx.collections.ArrayCollection;
			import mx.events.FlexEvent;
			
			import spark.components.DataGroup;
			import spark.events.IndexChangeEvent;
			
			import data.DataProviderListItem;
			
			import utils.GenericUtils;
			
			import view.interfaces.ISelectableItemsComponent;
			import view.primeFaces.surfaceComponents.components.DataTable;
			
			[Bindable] public var tableVar:String;
			
			private var _binding:ChangeWatcher;
			private var _label:String = "Items";
			
			[Bindable("labelChange")]
			public function get label():String
			{
				return this._label;
			}
			
			public function set label(value:String):void
			{
				if (this._label === value)
				{
					return;
				}
				this._label = value;
				this.dispatchEvent(new Event("labelChange"));
			}
			
			private function editSelectedItem():void
			{
				var item:DataProviderListItem = DataProviderListItem(this.itemsList.selectedItem);
				var group:DataGroup = this.itemsList.dataGroup;
				
				var itemCount:int = group.numElements;
				for (var i:int = 0; i < itemCount; i++)
				{
					var itemRenderer:DataTableColumnFormItemRenderer = group.getElementAt(i) as DataTableColumnFormItemRenderer;
					if (!itemRenderer)
					{
						continue;
					}
					if (itemRenderer.data === item)
					{
						itemRenderer.startEditing();
						break;
					}
				}
			}
			
			private function addItemButton_clickHandler(event:MouseEvent):void
			{
				var listItem:DataProviderListItem = new DataProviderListItem("Title");
				itemsList.dataProvider.addItem(listItem);
				
				onItemsListEditingFinished(null);
				GenericUtils.listScrollToBottom(itemsList);
				
				// make this separate function to 
				// access class level method while triggered
				// by callLater()
				itemsList.callLater(updateListsAndEdit, [listItem]);
			}
			
			private function updateListsAndEdit(value:DataProviderListItem):void
			{
				this.itemsList.selectedItem = value;
				editSelectedItem();
			}
			
			private function editItemButton_clickHandler(event:MouseEvent):void
			{
				this.editSelectedItem();
			}
			
			private function deleteItemButton_clickHandler(event:MouseEvent):void
			{
				itemsList.dataProvider.removeItemAt(itemsList.selectedIndex);
				
				onItemsListEditingFinished(null);
			}
			
			private function itemsList_changeHandler(event:IndexChangeEvent):void
			{
				if (selectedItem is ISelectableItemsComponent)
				{
					ISelectableItemsComponent(selectedItem).selectedIndex = event.newIndex;
				}
			}
			
			private function onTabFormCreationComplete(event:FlexEvent):void
			{
				itemsList.addEventListener("editingFinished", onItemsListEditingFinished);
				
				updateColumnList();
				updateBinding();
			}
			
			private function updateColumnList():void
			{
				var grid:DataTable = (selectedItem as DataTable);
				itemsList.dataProvider = grid.tableColumnDescriptor;
			}
			
			private function onItemsListEditingFinished(event:Event):void
			{
				var grid:DataTable = (selectedItem as DataTable);
				grid.tableColumnDescriptor = itemsList.dataProvider as ArrayCollection;
			}
			
			private function updateBinding():void
			{
				if (this._binding)
				{
					this._binding.unwatch();
					this._binding = null;
				}
				if (this.selectedItem === null)
				{
					return;
				}
				this._binding = BindingUtils.bindSetter(onTableVarUpdated, selectedItem, "tableVar", false, true);
			}
			
			private function onTableVarUpdated(value:Object):void
			{
				tableVar = value.toString();
				(selectedItem as DataTable).tableColumnDescriptor.refresh();
			}
			
		]]>
	</fx:Script>
	
    <suportclasses:layout>
        <s:FormLayout horizontalAlign="contentJustify"/>
    </suportclasses:layout>

    <s:FormItem label="{label}" skinClass="skins.StackedFormItemSkin">
        <s:List id="itemsList" width="100%" height="100%"
                dragEnabled="true" dragMoveEnabled="true" dropEnabled="true"
                itemRenderer="view.primeFaces.forms.DataTableColumnFormItemRenderer"
                change="itemsList_changeHandler(event)">
            <s:layout>
                <s:VerticalLayout gap="0" horizontalAlign="contentJustify"
                                  requestedRowCount="5" requestedMaxRowCount="5"/>
            </s:layout>
        </s:List>
        <s:HGroup>
            <s:Button width="100%" styleName="addDataProviderItemButton" toolTip="Add Item"
                      click="addItemButton_clickHandler(event)"/>
            <s:Button styleName="editDataProviderItemButton" toolTip="Edit Item"
                      enabled="{itemsList.selectedIndex !== -1}"
                      click="editItemButton_clickHandler(event)"/>
            <s:Button styleName="deleteDataProviderItemButton" toolTip="Delete Item"
                      enabled="{itemsList.selectedIndex !== -1 &amp;&amp; itemsList.dataProvider.length > 1}"
                      click="deleteItemButton_clickHandler(event)"/>
        </s:HGroup>
    </s:FormItem>
</suportclasses:BasePropertyEditorForm>