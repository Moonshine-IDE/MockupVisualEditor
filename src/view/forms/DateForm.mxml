<?xml version="1.0" encoding="utf-8"?>
<suportclasses:BasePropertyEditorForm
        xmlns:fx="http://ns.adobe.com/mxml/2009"
        xmlns:mx="library://ns.adobe.com/flex/mx"
        xmlns:s="library://ns.adobe.com/flex/spark" xmlns:suportclasses="view.suportClasses.*">

    <suportclasses:layout>
        <s:FormLayout horizontalAlign="contentJustify"/>
    </suportclasses:layout>

    <s:FormItem label="Date">
        <mx:DateField id="dateField" width="100%"
                      selectedDate="{bindableDate}" change="dateField_changeHandler(event)"/>
    </s:FormItem>

    <fx:Script><![CDATA[
        import mx.binding.utils.BindingUtils;
        import mx.binding.utils.ChangeWatcher;

        import view.interfaces.ISurfaceComponent;

        private static const FIELD_SELECTED_DATE:String = "selectedDate";

        [Bindable]
        public var bindableDate:Date;

        private var _binding:ChangeWatcher;

        override public function set selectedItem(value:ISurfaceComponent):void
        {
            super.selectedItem = value;
            this.updateBinding();
        }

        private function dateField_changeHandler(event:Event):void
        {
            selectedItem[FIELD_SELECTED_DATE] = this.dateField.selectedDate;
        }

        private function updateBinding():void
        {
            if (this._binding)
            {
                this._binding.unwatch();
                this._binding = null;
            }
            if (this.selectedItem === null)
            {
                return;
            }
            this._binding = BindingUtils.bindProperty(this, "bindableDate", this.selectedItem, FIELD_SELECTED_DATE, true);
        }
        ]]></fx:Script>
</suportclasses:BasePropertyEditorForm>
