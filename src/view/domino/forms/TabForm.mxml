<?xml version="1.0" encoding="utf-8"?>
<!--
////////////////////////////////////////////////////////////////////////////////
//
//  Copyright (C) STARTcloud, Inc. 2015-2022. All rights reserved.
//
//  This program is free software: you can redistribute it and/or modify
//  it under the terms of the Server Side Public License, version 1,
//  as published by MongoDB, Inc.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  Server Side Public License for more details.
//
//  You should have received a copy of the Server Side Public License
//  along with this program. If not, see
//
//  http://www.mongodb.com/licensing/server-side-public-license
//
//  As a special exception, the copyright holders give permission to link the
//  code of portions of this program with the OpenSSL library under certain
//  conditions as described in each individual source file and distribute
//  linked combinations including the program with the OpenSSL library. You
//  must comply with the Server Side Public License in all respects for
//  all of the code used other than as permitted herein. If you modify file(s)
//  with this exception, you may extend this exception to your version of the
//  file(s), but you are not obligated to do so. If you do not wish to do so,
//  delete this exception statement from your version. If you delete this
//  exception statement from all source files in the program, then also delete
//  it in the license file.
//
////////////////////////////////////////////////////////////////////////////////
-->
<suportclasses:BasePropertyEditorForm
        xmlns:fx="http://ns.adobe.com/mxml/2009"
        xmlns:s="library://ns.adobe.com/flex/spark"
        xmlns:suportclasses="view.suportClasses.*"
        skinClass="skins.FormSkin"
        creationComplete="onTabFormCreationComplete(event)">
	<fx:Script>
		<![CDATA[
		import mx.collections.ArrayList;
		import mx.events.FlexEvent;

		import view.primeFaces.supportClasses.NavigatorContent;
		import spark.containers.Navigator;
		import spark.events.GridItemEditorEvent;

		import data.DataProviderListItem;

		import mx.controls.Alert; 

		import view.interfaces.ISelectableItemsComponent;
		import view.interfaces.ISurfaceComponent;
		import view.domino.surfaceComponents.components.DominoTabView;

		[Bindable]
		private var itemsListDataProvider:ArrayList;

		private var _label:String = "Items";
		private var itemAdded:Boolean;

		[Bindable("labelChange")]
		public function get label():String
		{
			return this._label;
		}

		public function set label(value:String):void
		{
			if (this._label === value)
			{
				return;
			}
			this._label = value;
			this.dispatchEvent(new Event("labelChange"));
		}

		[Bindable("change")]
		protected function get selectedTabView():DominoTabView
		{
			return selectedItem as DominoTabView;
		}

		private function editSelectedItem():void
		{
			dgTabs.ensureCellIsVisible(dgTabs.selectedIndex);
			dgTabs.startItemEditorSession(dgTabs.selectedIndex, 0);
		}

		private function addItemButton_clickHandler(event:MouseEvent):void
		{
			var navContent:NavigatorContent = new NavigatorContent();
			navContent.addElement(selectedTabView.getNewDiv());
			navContent.label = "Title";

			var navigator:Navigator = (selectedItem as Navigator);
			navigator.addElement(navContent);
			var navigatorNumElements:int = navigator.numElements;

			var listItem:DataProviderListItem = new DataProviderListItem("Title");
			itemsListDataProvider.addItem(listItem);

			if (selectedItem is ISelectableItemsComponent)
			{
				ISelectableItemsComponent(selectedItem).selectedIndex = navigatorNumElements - 1;
			}

			this.itemAdded = true;
			this.dgTabs.invalidateDisplayList();
			this.invalidateDisplayList();
		}

		override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
		{
			super.updateDisplayList(unscaledWidth, unscaledHeight);

			if (itemAdded)
			{
				dgTabs.setSelectedIndex(dgTabs.dataProvider.length - 1);
				this.editSelectedItem();
				this.itemAdded = false;
			}
		}

		override public function set selectedItem(value:ISurfaceComponent):void
		{
			if (selectedItem) selectedItem.removeEventListener(DominoTabView.EVENT_CHILDREN_UPDATED, onTabViewChildrenUpdated);
			
			super.selectedItem = value;
			if (value) selectedItem.addEventListener(DominoTabView.EVENT_CHILDREN_UPDATED, onTabViewChildrenUpdated, false, 0, true);
		}

		private function editItemButton_clickHandler(event:MouseEvent):void
		{
			this.editSelectedItem();
		}

		private function deleteItemButton_clickHandler(event:MouseEvent):void
		{
			var deleteIndex:int = dgTabs.selectedIndex;

			var navigator:Navigator = (selectedItem as Navigator);
			if (navigator.numElements > 1)
			{
				navigator.removeItemAt(deleteIndex);
				dgTabs.dataProvider.removeItemAt(dgTabs.selectedIndex);
				dgTabs.selectedIndex = 0;
				dgTabs.ensureCellIsVisible(0);
			}
		}

		private function onTabFormCreationComplete(event:FlexEvent):void
		{
			dgTabs.columnHeaderGroup.visible = dgTabs.columnHeaderGroup.includeInLayout = false;

			if(selectedTabView==null){
				Alert.show("selectedTabView is null");
			}else{
				var navigatorNumElements:int = selectedTabView.numElements;
				itemsListDataProvider = new ArrayList();
				
				for (var i:int = 0; i < navigatorNumElements; i++)
				{
					var tabItem:NavigatorContent = selectedTabView.getElementAt(i) as NavigatorContent;
					var listItem:DataProviderListItem = new DataProviderListItem(tabItem.label);
					itemsListDataProvider.addItem(listItem);
				}
			

				dgTabs.dataProvider = itemsListDataProvider;
				dgTabs.selectedIndex = selectedTabView.selectedIndex;
				
				_childrenForFocus = [];
				_childrenForFocus.push(addTabBtn);
				_childrenForFocus.push(editTabBtn);
				_childrenForFocus.push(deleteTabBtn);
			}
		}

		private function onTabViewChildrenUpdated(event:Event):void
		{
			onTabFormCreationComplete(null);
		}

		protected function onItemEditFinished(event:GridItemEditorEvent):void
		{
			var listItem:DataProviderListItem = dgTabs.dataProvider.getItemAt(event.rowIndex) as DataProviderListItem;
			selectedTabView.setSelectedTabLabel(listItem.label);
		}
		]]>
	</fx:Script>
    <suportclasses:layout>
        <s:FormLayout horizontalAlign="contentJustify"/>
    </suportclasses:layout>

    <s:FormItem label="{label}" skinClass="skins.StackedFormItemSkin">
		<s:DataGrid id="dgTabs"
					width="100%" requestedMinRowCount="4" height="130"
					editable="true" sortableColumns="false" draggableColumns="false"
					selectedIndex="@{selectedTabView.selectedIndex}"
					gridItemEditorSessionSave="onItemEditFinished(event)"> <!-- one column header with grid doesn't looks great, so we'll keep this as listing only -->
			<s:columns>
				<s:ArrayList>
					<s:GridColumn dataField="label" headerText="Title"/>
				</s:ArrayList>
			</s:columns>
		</s:DataGrid>
        <s:HGroup>
            <s:Button id="addTabBtn" width="100%" styleName="addDataProviderItemButton" toolTip="Add Item"
                      click="addItemButton_clickHandler(event)"/>
            <s:Button id="editTabBtn" styleName="editDataProviderItemButton" toolTip="Edit Item"
                      enabled="{dgTabs.selectedIndex !== -1}"
                      click="editItemButton_clickHandler(event)"/>
            <s:Button id="deleteTabBtn" styleName="deleteDataProviderItemButton" toolTip="Delete Item"
                      enabled="{dgTabs.selectedIndex !== -1 &amp;&amp; dgTabs.dataProvider.length > 1}"
                      click="deleteItemButton_clickHandler(event)"/>
        </s:HGroup>
    </s:FormItem>
</suportclasses:BasePropertyEditorForm>
