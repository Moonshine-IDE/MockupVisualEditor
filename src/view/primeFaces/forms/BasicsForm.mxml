<?xml version="1.0" encoding="utf-8"?>
<!--
////////////////////////////////////////////////////////////////////////////////
//
//  Copyright (C) STARTcloud, Inc. 2015-2022. All rights reserved.
//
//  This program is free software: you can redistribute it and/or modify
//  it under the terms of the Server Side Public License, version 1,
//  as published by MongoDB, Inc.
//
//  This program is distributed in the hope that it will be useful,
//  but WITHOUT ANY WARRANTY; without even the implied warranty of
//  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
//  Server Side Public License for more details.
//
//  You should have received a copy of the Server Side Public License
//  along with this program. If not, see
//
//  http://www.mongodb.com/licensing/server-side-public-license
//
//  As a special exception, the copyright holders give permission to link the
//  code of portions of this program with the OpenSSL library under certain
//  conditions as described in each individual source file and distribute
//  linked combinations including the program with the OpenSSL library. You
//  must comply with the Server Side Public License in all respects for
//  all of the code used other than as permitted herein. If you modify file(s)
//  with this exception, you may extend this exception to your version of the
//  file(s), but you are not obligated to do so. If you do not wish to do so,
//  delete this exception statement from your version. If you delete this
//  exception statement from all source files in the program, then also delete
//  it in the license file.
//
////////////////////////////////////////////////////////////////////////////////
-->
<suportclasses:BasePropertyEditorForm xmlns:fx="http://ns.adobe.com/mxml/2009"
									  xmlns:s="library://ns.adobe.com/flex/spark"
									  xmlns:suportclasses="view.suportClasses.*"
									  skinClass="skins.FormSkin"
									  creationComplete="onBasicFormCreationComplete(event)"
									  stateChangeComplete="onBasicFormStateChangeComplete(event)"
									  currentState="normal">
	<suportclasses:states>
		<s:State name="normal"/>
		<s:State name="normalWithId"/>
	</suportclasses:states>

	<suportclasses:layout>
		<s:FormLayout />
	</suportclasses:layout>
	<s:FormItem label="Id" skinClass="skins.FormItemSkin" includeIn="normalWithId">
		<s:TextInput id="idTextInput" width="100%" skinClass="skins.TextInputSkin"
					 text="{selectedComponentIdAttributeChaged}" focusOut="commitId()"
					 height="25" tabIndex="1" keyDown="idInput_keyDownHandler(event)"/>
	</s:FormItem>
    
	<s:FormHeading label="Size" skinClass="skins.FormHeadingSkin"/>
	<s:FormItem label="Width" skinClass="skins.FormItemSkin">
		<s:HGroup width="100%">
			<s:TextInput id="widthInput" width="100%"
						 text="{selectedComponentComputedWidth}" skinClass="skins.TextInputSkin"
						 keyDown="widthInput_keyDownHandler(event)" height="25"
						 enabled="{widthOutputFlag}"
						 tabIndex="2" />
			<s:CheckBox id="widthOutput" visible="{selectedItem is IComponentSizeOutput}"
						includeInLayout="{selectedItem is IComponentSizeOutput}"
						selected="{widthOutputFlag}"
						change="onWidthOutputChange(event)"/>
		</s:HGroup>
	</s:FormItem>
	<s:FormItem label="Height" skinClass="skins.FormItemSkin">
		<s:HGroup width="100%">
			<s:TextInput id="heightInput" width="100%"
						 text="{selectedComponentComputedHeight}" skinClass="skins.TextInputSkin"
						 keyDown="heightInput_keyDownHandler(event)" height="25"
						 enabled="{heightOutputFlag}"
						 tabIndex="3" />
			<s:CheckBox id="heightOutput" visible="{selectedItem is IComponentSizeOutput}"
						includeInLayout="{selectedItem is IComponentSizeOutput}"
						selected="{heightOutputFlag}"
						change="onHeightOutputChange(event)"/>
		</s:HGroup>
	</s:FormItem>
	<fx:Script><![CDATA[
		import interfaces.IComponentPercentSizeOutput;
		import interfaces.IComponentSizeOutput;

		import mx.binding.utils.BindingUtils;
        import mx.binding.utils.ChangeWatcher;
        import mx.core.UIComponent;
        import mx.events.FlexEvent;

        import utils.GenericUtils;

        import view.interfaces.IIdAttribute;
        import view.interfaces.ISurfaceComponent;

        [Bindable]
        public var widthOutputFlag:Boolean = true;
        [Bindable]
        public var heightOutputFlag:Boolean = true;

        private var _bindingWidth:ChangeWatcher;
        private var _bindingHeight:ChangeWatcher;
        private var _bindingId:ChangeWatcher;
        private var _widthOutput:ChangeWatcher;
        private var _heightOutput:ChangeWatcher;

        [Bindable("change")]
        private function get selectedComponent():UIComponent
        {
            //x, y, width, and height aren't bindable on interfaces used by
            //UIComponent, so we need to give the compiler a little hint here
            return UIComponent(this.selectedItem);
        }

        override public function set selectedItem(value:ISurfaceComponent):void
        {
            if (super.selectedItem != value)
            {
                super.selectedItem = value;
                this.currentState = value is IIdAttribute ? "normalWithId" : "normal";
                updateBinding();
            }
        }

        [Bindable("propertyEditorChanged")]
        public function get selectedComponentComputedWidth():String
        {
            var percentSizeComponent:IComponentPercentSizeOutput = selectedComponent as IComponentPercentSizeOutput;
            if (!isNaN(selectedComponent.percentWidth) || percentSizeComponent)
            {
                if (percentSizeComponent && !isNaN(percentSizeComponent.widthPercent))
                {
                    return percentSizeComponent.widthPercent + "%";
                }
                else if (!isNaN(selectedComponent.percentWidth))
                {
                    return selectedComponent.percentWidth + "%";
                }
            }

            return selectedComponent.width.toString();
        }

        [Bindable("propertyEditorChanged")]
        public function get selectedComponentComputedHeight():String
        {
            var percentSizeComponent:IComponentPercentSizeOutput = selectedComponent as IComponentPercentSizeOutput;
            if (!isNaN(selectedComponent.percentHeight) || percentSizeComponent)
            {
                if (percentSizeComponent && !isNaN(percentSizeComponent.heightPercent))
                {
                    return percentSizeComponent.heightPercent + "%";
                }
                else if (!isNaN(selectedComponent.percentHeight))
                {
                    return selectedComponent.percentHeight + "%";
                }
            }

            return selectedComponent.height.toString();
        }

        [Bindable("propertyEditorChanged")]
        public function get selectedComponentIdAttributeChaged():String
        {
            if (this.selectedItem is IIdAttribute)
            {
                return this.selectedItem["idAttribute"];
            }

            return "";
        }

        private function commitWidth():void
        {
            if (!this.selectedItem) return;

            if (this.widthInput.text.indexOf("%") != -1)
            {
                GenericUtils.applyPercentageWidthHeight(this.selectedItem, this.widthInput.text, this.heightInput.text, true);
            }
            else
            {
                var newWidth:Number = parseFloat(this.widthInput.text);
                this.widthInput.text = GenericUtils.applyMinAndMaxWidth(this.selectedItem, newWidth);
            }
            this.widthInput.selectAll();
        }

        private function commitHeight():void
        {
            if (!this.selectedItem) return;

            if (this.heightInput.text.indexOf("%") != -1)
            {
                GenericUtils.applyPercentageWidthHeight(this.selectedItem, this.widthInput.text, this.heightInput.text, false);
            }
            else
            {
                var newHeight:Number = parseFloat(this.heightInput.text);
                this.heightInput.text = GenericUtils.applyMinAndMaxHeight(this.selectedItem, newHeight);
            }
            this.heightInput.selectAll();
        }

        private function commitId():void
        {
            if (!this.selectedItem) return;

            this.selectedItem["idAttribute"] = this.idTextInput.text;
        }

        private function widthInput_keyDownHandler(event:KeyboardEvent):void
        {
            if (event.keyCode === Keyboard.ENTER || event.keyCode == Keyboard.TAB)
            {
                commitWidth();
            }
        }

        private function heightInput_keyDownHandler(event:KeyboardEvent):void
        {
            if (event.keyCode === Keyboard.ENTER || event.keyCode == Keyboard.TAB)
            {
                commitHeight();
            }
        }

        private function idInput_keyDownHandler(event:KeyboardEvent):void
        {
            if (event.keyCode === Keyboard.ENTER || event.keyCode == Keyboard.TAB)
            {
                commitId();
            }
        }

        private function onBasicFormCreationComplete(event:FlexEvent):void
        {
            removeEventListener(FlexEvent.CREATION_COMPLETE, onBasicFormCreationComplete);

            this.refreshChildrenForFocus();
            this.refreshWidthInputEnabled(selectedItem);
        }

        private function updateBinding():void
        {
            if (this._bindingWidth)
            {
                this._bindingWidth.unwatch();
                this._bindingHeight.unwatch();
                this._bindingWidth = this._bindingHeight = null;

                if (currentState == "normalWithId")
                {
                    this._bindingId.unwatch();
                    this._bindingId = null;
                }
            }
            if (this._widthOutput)
            {
                this._widthOutput.unwatch();
                this._heightOutput.unwatch();
                this._widthOutput = this._heightOutput = null;
            }
            if (this.selectedItem === null)
            {
                return;
            }
            this._bindingWidth = BindingUtils.bindSetter(onWidthUpdated, selectedItem, "width", false, true);
            this._bindingHeight = BindingUtils.bindSetter(onHeightUpdated, selectedItem, "height", false, true);
            if (currentState == "normalWithId") this._bindingId = BindingUtils.bindSetter(onIdUpdated, selectedItem, "idAttribute", false, true);

            if (selectedItem is IComponentSizeOutput)
            {
                this._widthOutput = BindingUtils.bindProperty(this, "widthOutputFlag", this.selectedItem, "widthOutput", false, true);
                this._heightOutput = BindingUtils.bindProperty(this, "heightOutputFlag", this.selectedItem, "heightOutput", false, true);
            }
        }

        private function onWidthUpdated(value:Object):void
        {
            if (!this.selectedItem) return;

            if (this.widthInput)
            {
                this.widthInput.text = selectedComponentComputedWidth;
            }
        }

        private function onHeightUpdated(value:Object):void
        {
            if (!this.selectedItem) return;

            if (this.heightInput)
            {
                this.heightInput.text = selectedComponentComputedHeight;
            }
        }

        private function onIdUpdated(value:Object):void
        {
            if (!this.selectedItem) return;

            if (this.idTextInput)
            {
                this.idTextInput.text = this.selectedItem["idAttribute"];
            }
        }

        private function onBasicFormStateChangeComplete(event:FlexEvent):void
        {
            this.refreshChildrenForFocus();
        }

        private function refreshChildrenForFocus():void
        {
            if (currentState == "normal")
            {
                _childrenForFocus = [];
                _childrenForFocus.push(widthInput.textDisplay);
                _childrenForFocus.push(heightInput.textDisplay);
            }
            else if (currentState == "normalWithId")
            {
                _childrenForFocus = [];
                _childrenForFocus.push(idTextInput.textDisplay);
                _childrenForFocus.push(widthInput.textDisplay);
                _childrenForFocus.push(heightInput.textDisplay);
            }
        }

        private function onWidthOutputChange(event:Event):void
        {
            //this.widthInput.enabled = this.widthOutput.selected;
            this.selectedItem["widthOutput"] = this.widthOutput.selected;
        }

        private function onHeightOutputChange(event:Event):void
        {
            //this.heightInput.enabled = this.heightOutput.selected;
            this.selectedItem["heightOutput"] = this.heightOutput.selected;
        }

        private function refreshWidthInputEnabled(value:ISurfaceComponent):void
        {
            if (!value) return;

            if (value is IComponentSizeOutput)
            {
                var componentSize:IComponentSizeOutput = (value as IComponentSizeOutput);
                widthOutputFlag = componentSize.widthOutput;
                heightOutputFlag = componentSize.heightOutput;
            }
        }
        ]]></fx:Script>
</suportclasses:BasePropertyEditorForm>