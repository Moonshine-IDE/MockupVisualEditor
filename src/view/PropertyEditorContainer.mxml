<?xml version="1.0" encoding="utf-8"?>
<s:BorderContainer xmlns:fx="http://ns.adobe.com/mxml/2009"
	xmlns:s="library://ns.adobe.com/flex/spark"
	currentState="empty" skinClass="skins.PropertyEditorContainerSkin" xmlns:view="view.*">
	<s:layout>
		<s:VerticalLayout verticalAlign="top" horizontalAlign="left"
						  verticalAlign.empty="middle" horizontalAlign.empty="center" />
	</s:layout>
	<fx:Metadata>
		[Event(name="change",type="flash.events.Event")]
		[Event(name="propertyEditorChanged",type="view.suportClasses.events.PropertyEditorChangeEvent")]
		[Event(name="propertyEditorItemDeleting",type="view.suportClasses.events.PropertyEditorChangeEvent")]
	</fx:Metadata>

	<s:states>
		<s:State name="empty"/>
		<s:State name="selection"/>
	</s:states>
	
	<s:Label text="No component selected" excludeFrom="selection" left="0" right="0"/>
	<view:PropertyEditorHeader id="propertyEditorHeader" excludeFrom="empty"/>
	<s:Group id="editorHolder" percentWidth="100" excludeFrom="empty"/>

	<fx:Script><![CDATA[
		import view.flex.propertyEditors.GenericPropertyEditor;
		import view.interfaces.IPropertyEditor;
		import view.interfaces.ISurfaceComponent;
		import view.interfaces.IVisualEditorLibraryBridge;
		
        private var _editor:IPropertyEditor;

        private var _selectedItem:ISurfaceComponent = null;

        [Bindable("change")]
        public function get selectedItem():ISurfaceComponent
        {
            return this._selectedItem;
        }

        public function set selectedItem(value:ISurfaceComponent):void
        {
            if (this._selectedItem === value)
            {
                return;
            }
            this._selectedItem = value;
            if (this._editor)
            {
				editorHolder.removeElement(this._editor);
                this._editor = null;
            }
            if (this._selectedItem !== null)
            {
                this.currentState = "selection";
                var PropertyEditorType:Class = this._selectedItem.propertyEditorClass;
                if (PropertyEditorType === null)
                {
                    PropertyEditorType = GenericPropertyEditor;
                }
                this._editor = new PropertyEditorType();
                this._editor.top = 0;
                this._editor.right = 0;
                this._editor.bottom = 0;
                this._editor.left = 0;
                this._editor.selectedItem = value;
				editorHolder.addElement(this._editor);

				propertyEditorHeader.componentName = selectedItem["className"];
            }
            else
            {
                this.currentState = "empty";
				propertyEditorHeader.componentName = "";
            }
            this.dispatchEvent(new Event(Event.CHANGE));
        }
		
		private var _visualEditorFilePath:String;
		public function set visualEditorFilePath(value:String):void
		{
			_visualEditorFilePath = value;
		}
		public function get visualEditorFilePath():String
		{
			return _visualEditorFilePath;
		}
		
		private var _moonshineBridge:IVisualEditorLibraryBridge;
		public function set moonshineBridge(value:IVisualEditorLibraryBridge):void
		{
			_moonshineBridge = value;
		}
		public function get moonshineBridge():IVisualEditorLibraryBridge
		{
			return _moonshineBridge;
		}
        ]]></fx:Script>
</s:BorderContainer>
