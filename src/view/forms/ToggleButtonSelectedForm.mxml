<?xml version="1.0" encoding="utf-8"?>
<suportclasses:BasePropertyEditorForm xmlns:fx="http://ns.adobe.com/mxml/2009"
                                      xmlns:s="library://ns.adobe.com/flex/spark"
                                      xmlns:suportclasses="view.suportClasses.*"
                                      skinClass="skins.FormSkin">

    <suportclasses:layout>
        <s:FormLayout />
    </suportclasses:layout>

    <s:FormItem label="{label}" skinClass="skins.FormItemSkin">
        <s:CheckBox id="check"
                    selected="{bindableText}"
                    change="check_changeHandler(event)"/>
    </s:FormItem>

    <fx:Script><![CDATA[
        import mx.binding.utils.BindingUtils;
        import mx.binding.utils.ChangeWatcher;

        import view.interfaces.ISurfaceComponent;

        private var selectedDirty:Boolean;
        private var _binding:ChangeWatcher;

        [Bindable]
        public var bindableText:Boolean;
        private var _label:String = "Text";

        [Bindable("labelChange")]
        public function get label():String
        {
            return this._label;
        }

        public function set label(value:String):void
        {
            if (this._label === value)
            {
                return;
            }
            this._label = value;
            this.dispatchEvent(new Event("labelChange"));
        }

        private var _textProperty:String = "text";

        [Bindable("change")]
        public function get textProperty():String
        {
            return this._textProperty;
        }

        public function set textProperty(value:String):void
        {
            if (this._textProperty === value)
            {
                return;
            }
            this._textProperty = value;
            this.updateBinding();
            this.dispatchEvent(new Event(Event.CHANGE));
        }

        override public function set selectedItem(value:ISurfaceComponent):void
        {
            if (super.selectedItem != value)
            {
                super.selectedItem = value;

                if (value)
                {
                    this.selectedDirty = true;
                }
            }
        }

        override protected function updateDisplayList(unscaledWidth:Number, unscaledHeight:Number):void
        {
            super.updateDisplayList(unscaledWidth, unscaledHeight);

            if (this.check && this.selectedDirty)
            {
                this.check.selected = this.selectedItem[_textProperty];
                this.selectedDirty = false;
            }
        }

        private function check_changeHandler(event:Event):void
        {
            if (!(this.textProperty in this.selectedItem))
            {
                this.check.errorString = "Field " + this._textProperty + " does not exist";
                return;
            }
            this.check.errorString = "";

            this.selectedItem[this._textProperty] = this.check.selected;
        }

        private function updateBinding():void
        {
            if (this._binding)
            {
                this._binding.unwatch();
                this._binding = null;
            }
            if (this.selectedItem === null)
            {
                return;
            }
            this._binding = BindingUtils.bindProperty(this, "bindableText", this.selectedItem, this._textProperty, true);
        }
        ]]></fx:Script>
</suportclasses:BasePropertyEditorForm>
